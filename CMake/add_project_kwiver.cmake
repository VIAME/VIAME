# kwiver External Project
#
# Required symbols are:
#   VIAME_BUILD_PREFIX - where packages are built
#   VIAME_BUILD_INSTALL_PREFIX - directory install target
#   VIAME_PACKAGES_DIR - location of git submodule packages
#   VIAME_ARGS_COMMON -
##

set( VIAME_PROJECT_LIST ${VIAME_PROJECT_LIST} kwiver )

if( VIAME_ENABLE_MATLAB )
  FormatPassdowns( "MATLAB" VIAME_MATLAB_FLAGS )
endif()

if( VIAME_ENABLE_PYTHON )
  FormatPassdowns( "PYTHON" VIAME_PYTHON_FLAGS )
endif()

set( VIAME_ENABLE_SPROKIT ON )

if( MSVC AND MSVC_VERSION EQUAL 1900 )
  set( VIAME_ENABLE_SPROKIT OFF )
endif()

ExternalProject_Add(kwiver
  DEPENDS fletch
  PREFIX ${VIAME_BUILD_PREFIX}
  SOURCE_DIR ${VIAME_PACKAGES_DIR}/kwiver
  CMAKE_GENERATOR ${gen}
  CMAKE_ARGS
    ${VIAME_ARGS_COMMON}
    ${VIAME_ARGS_fletch}
    ${VIAME_MATLAB_FLAGS}
    ${VIAME_PYTHON_FLAGS}

    # Required
    -DKWIVER_ENABLE_ARROWS:BOOL=ON
    -DKWIVER_ENABLE_TOOLS:BOOL=ON
    -DKWIVER_ENABLE_SPROKIT:BOOL=${VIAME_ENABLE_SPOKIT}
    -DKWIVER_ENABLE_PROCESSES:BOOL=ON
    -DKWIVER_INSTALL_SET_UP_SCRIPT:BOOL=OFF

    # Optional
    -DKWIVER_ENABLE_OPENCV:BOOL=${VIAME_ENABLE_OPENCV}
    -DKWIVER_ENABLE_VXL:BOOL=${VIAME_ENABLE_VXL}
    -DKWIVER_ENABLE_MATLAB:BOOL=${VIAME_ENABLE_MATLAB}
    -DKWIVER_ENABLE_C_BINDINGS:BOOL=${VIAME_ENABLE_PYTHON}
    -DKWIVER_ENABLE_PYTHON:BOOL=${VIAME_ENABLE_PYTHON}
    -DKWIVER_ENABLE_DOCS:BOOL=${VIAME_ENABLE_DOCS}
    -DKWIVER_INSTALL_DOCS:BOOL=${VIAME_ENABLE_DOCS}
    -DKWIVER_ENABLE_TRACK_ORACLE:BOOL=${VIAME_ENABLE_KWANT}

  INSTALL_DIR ${VIAME_BUILD_INSTALL_PREFIX}
  )

ExternalProject_Add_Step(kwiver forcebuild
  COMMAND ${CMAKE_COMMAND}
    -E remove ${VIAME_BUILD_PREFIX}/src/kwiver-stamp/kwiver-build
  COMMENT "Removing build stamp file for build update (forcebuild)."
  DEPENDEES configure
  DEPENDERS build
  ALWAYS 1
  )

set(VIAME_ARGS_kwiver
  -Dkwiver_DIR:PATH=${VIAME_BUILD_PREFIX}/src/kwiver-build
  )
